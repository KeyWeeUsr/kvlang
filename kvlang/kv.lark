start: (_NL | special | widget_tree | widget_rule_tree)*

special: "#:" special_directive _NL?
special_directive: kivy_version
    | kv_version
    | import
    | set

version: V_MAJOR "." V_MINOR ("." V_PATCH)? (V_PRE_RELEASE)? (V_BUILD_VERSION)?
V_MAJOR: /\d+/
V_MINOR: /\d+/
V_PATCH: /\d+/
V_PRE_RELEASE: "rc" | "a" | "b" | ".dev" | ".post"
V_BUILD_VERSION: /\d+/

kivy_version: "kivy" WHITESPACE version
kv_version: "kv" WHITESPACE version
IMPORT_NAME: ANY
IMPORT_MODULE: ANY
import: "import" IMPORT_NAME WHITESPACE IMPORT_MODULE

SET_NAME: ANY
SET_VALUE: ANY
set: "set" SET_NAME WHITESPACE SET_VALUE

widget_rule_tree: widget_rule _NL* (_INDENT (widget_tree | widget_property | canvas_tree)+ _DEDENT)?
widget_rule: "<" WIDGET_RULE ">" ":"?
WIDGET_RULE: /[A-Za-z0-9]+/
widget_tree: widget _NL* (_INDENT (widget_tree | widget_property | canvas_tree)+ _DEDENT)?
widget: WIDGET ":"?
widget_property: PROPERTY_NAME ":" PROPERTY_VALUE _NL?

canvas_tree: canvas ":" _NL* (_INDENT canvas_instruction_tree* _DEDENT)?
canvas: CANVAS
    | CANVAS_BEFORE
    | CANVAS_AFTER
CANVAS: "canvas"
CANVAS_BEFORE: CANVAS ".before"
CANVAS_AFTER: CANVAS ".after"
canvas_instruction_property: PROPERTY_NAME ":" PROPERTY_VALUE _NL?
canvas_instruction_tree: canvas_instruction _NL* (_INDENT canvas_instruction_property* _DEDENT)?
canvas_instruction: CANVAS_INSTRUCTION ":"? _NL?

CANVAS_INSTRUCTION: "ApplyContextMatrix" // 1.6.0+
    | "Bezier" // 1.0.8+
    | "BindTexture"
    | "BorderImage"
    | "BoxShadow" // 2.2.0+
    | "Callback"
    | "ChangeState" // 1.6.0+
    | "ClearBuffers" // 1.3.0+
    | "ClearColor" // 1.3.0+
    | "Color"
    | "Ellipse"
    | "Fbo"
    | "Line"
    | "LoadIdentity"
    | "Mesh"
    | "Point"
    | "PopMatrix"
    | "PopState"
    | "PushMatrix"
    | "PushState"
    | "Quad"
    | "Rectangle"
    | "Rotate"
    | "RoundedRectangle"
    | "Scale"
    | "SmoothEllipse" // 2.3.0+
    | "SmoothLine" // 1.9.0+
    | "SmoothQuad" // 2.3.0+
    | "SmoothRectangle" // 2.3.0+
    | "SmoothRoundedRectangle" // 2.3.0+
    | "SmoothTriangle" // 2.3.0+
    | "StencilPush"
    | "StencilPop"
    | "StencilPush"
    | "StencilUnUse"
    | "StencilUse"
    | "Translate"
    | "Triangle"
    | "UpdateNormalMatrix"
WIDGET: /(?!CANVAS)[A-Z][A-Za-z0-9]+/
PROPERTY_NAME: /[a-z_]+/
PROPERTY_VALUE: ANY
COMMENT: /\s*#(?!:)[^\n]*/

line: /[^\n]+/

// only (multi-)space supported in SPECIAL
WHITESPACE: " "+
ANY: /\S+/
_NL: (/\r?\n[\t ]*/)+

%ignore /[ \t\f]+/
%ignore COMMENT
%declare _INDENT _DEDENT
